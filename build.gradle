plugins {
    id 'org.springframework.boot' version '1.5.13.RELEASE'
    id 'com.google.protobuf' version '0.8.5'
    id 'java'
}

group = 'com.xuexiang'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.google.protobuf'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    mybatisGenerator
}
repositories {
    mavenCentral()
}

def grpcVersion = '1.19.0'
def protobufVersion = '3.6.1'
def protocVersion = protobufVersion
//自动生成proto文件
protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

dependencies {
    //grpc
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation 'io.github.lognet:grpc-spring-boot-starter:3.2.2'

    //spring boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // token令牌
    implementation 'io.jsonwebtoken:jjwt:0.9.0'

    //物理分页
    implementation('com.github.pagehelper:pagehelper-spring-boot-starter:1.1.2')
    //数据库连接
    implementation('org.springframework.boot:spring-boot-starter-jdbc')
    implementation('mysql:mysql-connector-java:5.1.35')
    implementation('com.alibaba:fastjson:1.2.47')
    implementation('com.alibaba:druid:1.0.11')
    implementation('com.alibaba:druid-spring-boot-starter:1.1.0')

    //mybatis自动生成代码
    mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.5'
    mybatisGenerator 'tk.mybatis:mapper:3.3.9'
    implementation('tk.mybatis:mapper-spring-boot-starter:1.2.4')

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}


apply from: './generator.gradle'
